IF EXISTS (SELECT * FROM sysobjects WHERE type = 'P' AND name = 'usp_NotifyCertifiers')
	BEGIN
		DROP  Procedure  usp_NotifyCertifiers
	END

GO

CREATE Procedure usp_NotifyCertifiers

AS

DECLARE @dayInt int
SET @dayInt = (SELECT DATEPART(DAY, GETDATE()))

IF (@dayInt <> 10)
BEGIN
	print 'Only notify certifiers on the 10th of each month'
	RETURN
END

--Get all 'certifiers' (supervisor or delegate if available) who have any pending review records

DECLARE @MailList CURSOR
SET @MailList = CURSOR FOR

SELECT Users.Email
FROM Users
WHERE UserId IN (
	SELECT     
			DISTINCT
			ISNULL(Users.DelegateID, Users.SupervisorID) as CertifierID
	FROM         Records INNER JOIN
						  Users ON Records.UserId = Users.UserId INNER JOIN
						  Status ON Records.StatusID = Status.ID
	WHERE     (Status.Name = 'PendingReview')
)

OPEN @MailList

DECLARE @Email varchar(50)
FETCH NEXT FROM @MailList INTO @Email

WHILE (@@FETCH_STATUS = 0)
	BEGIN
		--Send Emails on some date (TBD) to those who should have submitted a time record and didn't 
		DECLARE @bodyText varchar(MAX)
		
		SET @bodyText = 'This email was generated by the FSNEP Online Time Record System.
*** Please do not respond to this email address ***

This is a reminder that, as a certifying supervisor, you have pending FSNEP time record certifications which are due by the 15th of this month.  Please access the FSNEP Online Time Record System to review and certify all submitted time records by the 15th of this month.

If you have any questions regarding this message, or about time records in general, please contact your State Office analyst:

Corinne Gould ( cgould@ucdavis.edu ) or
Susan Padgett ( sdpadgett@ucdavis.edu ) or
Yolanda Cortez (yCortez@ucdavis.edu ).

FSNEP Online Record System

https://secure.caes.ucdavis.edu/FSNEPRecords
'

		EXEC msdb.dbo.sp_send_dbmail
			@recipients=@Email,
			@subject='UC-FSNEP Time Record Certifier reminder',
			@body=@bodyText
 
		FETCH NEXT FROM @MailList INTO @Email
	END

CLOSE @MailList
DEALLOCATE @MailList

GO


GRANT EXEC ON usp_NotifyCertifiers TO PUBLIC

GO


